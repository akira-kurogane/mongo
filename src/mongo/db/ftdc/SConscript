# -*- mode: python -*-
Import("env")
Import("get_option")

env = env.Clone()

ftdcEnv = env.Clone()
ftdcEnv.InjectThirdPartyIncludePaths(libraries=['zlib'])

ftdcEnv.Library(
    target='ftdc',
    source=[
        'block_compressor.cpp',
        'collector.cpp',
        'compressor.cpp',
        'controller.cpp',
        'decompressor.cpp',
        'file_manager.cpp',
        'file_reader.cpp',
        'file_writer.cpp',
        'util.cpp',
        'varint.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/bson/util/bson_extract',
        '$BUILD_DIR/mongo/db/server_options_core',
        '$BUILD_DIR/mongo/db/service_context',
        '$BUILD_DIR/third_party/s2/s2', # For VarInt
        '$BUILD_DIR/third_party/shim_zlib',
    ],
)

platform_libs = []

if env.TargetOSIs('linux'):
    platform_libs = [
        '$BUILD_DIR/mongo/util/procparser'
    ]
elif env.TargetOSIs('windows'):
    platform_libs = [
        '$BUILD_DIR/mongo/util/perfctr_collect'
    ]

env.Library(
    target='ftdc_server',
    source=[
        'ftdc_server.cpp',
        'ftdc_system_stats.cpp',
        'ftdc_system_stats_${TARGET_OS}.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/db/commands',
        '$BUILD_DIR/mongo/db/server_parameters',
        '$BUILD_DIR/mongo/util/processinfo',
        'ftdc'
    ] + platform_libs,
)

env.Library(
    target='ftdc_mongod',
    source=[
        'ftdc_commands.cpp',
        'ftdc_mongod.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/repl/repl_coordinator_interface',
        '$BUILD_DIR/mongo/db/storage/storage_options',
        'ftdc_server'
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/db/auth/auth',
        '$BUILD_DIR/mongo/db/auth/authprivilege',
    ],
)

env.Library(
    target='ftdc_mongos',
    source=[
        'ftdc_mongos.cpp',
    ],
    LIBDEPS_PRIVATE=[
        'ftdc_server'
    ],
)

env.CppUnitTest(
    target='ftdc_test',
    source=[
        'compressor_test.cpp',
        'controller_test.cpp',
        'file_manager_test.cpp',
        'file_writer_test.cpp',
        'ftdc_test.cpp',
        'util_test.cpp',
        'varint_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/service_context_test_fixture',
        '$BUILD_DIR/mongo/util/clock_source_mock',
        'ftdc',
    ],
)

env.Append(CXXFLAGS='-g')
ftdc_dump = env.Program(
    target='ftdc_dump',
    source=[
        'prometheus_renaming.cpp',
        'ftdc_process_metrics.cpp',
        'ftdc_workspace.cpp',
        'ftdc_dump.cpp',
    ],
    LIBDEPS=[
        'ftdc',
        '$BUILD_DIR/mongo/db/bson/dotted_path_support',
        '$BUILD_DIR/third_party/shim_pcrecpp',
    ],
    CCFLAGS=['-std=c++17'],
)

hygienic = get_option('install-mode') == 'hygienic'

if hygienic:
    env.Default('$INSTALL_DIR/bin/ftdc_dump$PROGSUFFIX')
else:
    env.Default(env.Install('#/', ftdc_dump))
